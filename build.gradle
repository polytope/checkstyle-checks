plugins {
    id 'java-library'
    id 'maven-publish'
}

apply from: 'https://raw.githubusercontent.com/passsy/gradle-GitVersioner/master/git-versioner.gradle'

ext.gitVersioner = [
        yearFactor: 1200,
]

group 'dk.polytope'
version gitVersion.version

sourceCompatibility = 1.8

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': gitVersion.version
        )
    }
}

publishing {
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/polytope/checkstyle-checks")
            credentials {
                username = System.getenv("GITHUB_MVN_USERNAME") ?: System.getenv("MVN_USERNAME_GITHUB")
                password = System.getenv("GITHUB_MVN_TOKEN") ?: System.getenv("MVN_TOKEN_GITHUB")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            pom {
                name = 'Checkstyle Checks'
                description = 'a collection of Checkstyle Checks which are used in Polytope\'s codebases.'
                url = 'https://github.com/polytope/checkstyle-checks'
            }

            from components.java
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.puppycrawl.tools:checkstyle:8.36'
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.6.2")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.6.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.6.2")

    testImplementation "org.mockito:mockito-core:2.+"
}

test {
    useJUnitPlatform()
}

tasks.create("generatePomPropertiesForJar") {
    File outputDirectory = file("$buildDir/pom-properties")
    outputDirectory.mkdirs()
    File pomProperties = file("$outputDirectory/pom.properties")
    pomProperties.text = """
#Generated by Gradle
#${new Date()}
groupId=${project.group}
artifactId=${project.name}
version=${project.version}
"""
    outputs.file(pomProperties)
}

jar {
    dependsOn("generatePomFileForMavenJavaPublication", "generatePomPropertiesForJar")
    into("META-INF/maven/${project.group}/${project.name}") {
        from(generatePomFileForMavenJavaPublication) {
            rename(".+", "pom.xml")
        }
        from(generatePomPropertiesForJar)
    }
}
